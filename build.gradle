apply plugin: 'groovy'
apply plugin: 'war'
apply plugin: 'com.bmuschko.tomcat'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.0'
    }
}

version = 0.1
sourceCompatibility = 1.7

ext {
    commonsDbcpVersion = '1.4'
    wicketVersion = '7.0.0-M4'
    slf4jVersion = '1.7.7'
    log4jVersion = '1.2.17'
    groovyVersion = '2.3.6' // This must match the version shipped with Gradle
    hibernateVersion = '4.3.7.Final'
    mysqlVersion = '5.1.34'
    spockVersion = '0.7-groovy-2.0'
    javaxServletVersion = '3.1.0'
    springVersion = '4.1.2.RELEASE'
    springBootVersion = '1.1.9.RELEASE'
    tomcatVersion = '7.0.11'
    hsqlVersion = '2.3.2'

    libs = [
            database: [
                  "commons-dbcp:commons-dbcp:$commonsDbcpVersion",
                  "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion",
                  "org.hibernate:hibernate-core:$hibernateVersion",
                  "mysql:mysql-connector-java:$mysqlVersion"
            ],
            logging: [
                    "org.slf4j:slf4j-log4j12:$slf4jVersion",
                    "log4j:log4j:$log4jVersion"
            ],
            spock: [
                    "org.spockframework:spock-core:$spockVersion",
                    "org.spockframework:spock-spring:$spockVersion"
            ],
            spring : [
                    "org.springframework:spring-core:$springVersion",
                    "org.springframework:spring-context:$springVersion",
                    "org.springframework:spring-web:$springVersion"
            ],
            tomcat : [
                    "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
                    "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}",
                    dependencies.create("org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}") {
                        exclude group: 'org.eclipse.jdt.core.compiler', module: 'ecj'
                    }
            ],
            wicket : [
                    "org.apache.wicket:wicket:$wicketVersion",
                    "org.apache.wicket:wicket-extensions:$wicketVersion",
                    "org.apache.wicket:wicket-spring:$wicketVersion"
            ]
    ]
}

sourceSets {
    main {
        groovy {
            resources {
                srcDir 'src/main/resources'
                srcDir 'src/main/groovy'
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {

    providedCompile "javax.servlet:javax.servlet-api:$javaxServletVersion"

    compile "org.codehaus.groovy:groovy-all:$groovyVersion"
    compile libs.database
    compile libs.wicket
    compile libs.logging
    compile libs.spring

    testCompile libs.spock
    testCompile "org.springframework:spring-test:$springVersion"
    testCompile "org.hsqldb:hsqldb:$hsqlVersion"

    tomcat libs.tomcat
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2'
}
